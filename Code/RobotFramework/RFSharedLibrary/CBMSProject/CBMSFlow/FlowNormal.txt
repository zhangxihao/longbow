*** Settings ***
Documentation     * 主干流程。
...
...               后续需要添加从开单--审核--二结--......--交易完成，的所有正常流程关键字。用于层层嵌套，层层调用。达到减少测试用例的冗余。并提供给分支流程使用
...
...               这样做的目的是为了后续需要用到某一环节的时候只需要调用其中一个流程。
...               例如有流程：123456789，当需要对第6个流程做分支处理时，在case的Setup中先调用流程5，流程5会自动调用12345，然后在去6里面做具体的逻辑
Resource          Flow.txt
Resource          ../ProjectResource.txt

*** Keywords ***
流程登录正常
    [Documentation]    - 交易员登录流程
    流程登录    ${URL.Login}    ${User.UsernameJiaoyiyuan}    ${User.PasswordJiaoyiyuan}

流程开单正常
    流程登录正常
    流程新开交易单-录入买家信息    ${Account.BuyerName}    ${Account.BuyerDepartment}    ${Account.BuyerContant}    ${Account.BuyerContantTel}
    流程新开交易单-录入资源信息    ${Account.SellerName}    ${Account.SellerDepartment}    ${Account.SellerContant}    中厚板    Q235A    1100*1200*1300
    ...    测试厂家1    湖北省    武汉市    测试仓库1    1    1
    ...    抄码    1    1    无
    单击按钮    xpath=//*[@id="submit"]    #提交审核，这个操作上面可能会有很多组合流程，所以放测试用例里面执行
    流程获取新开单后的交易单号
    关闭当前浏览器

流程审核正常
    流程开单正常
    Comment    Start new flow
    流程登录    ${URL.Login}    ${User.UsernameHangzhouowner}    ${User.PasswordHangzhouowner}
    流程交易单管理-待审核Tab-审核
    流程交易单管理-待审核Tab-审核通过    自动化测试执行-待审核备注信息

流程关联正常
    流程审核正常
    Comment    Start new flow
    流程登录    ${URL.Login}    ${User.UsernameJiaoyiyuan}    ${User.PasswordJiaoyiyuan}
    流程交易单管理-待关联Tab
    流程交易单管理-待关联Tab-关联

流程申请付款正常
    流程关联正常
    Comment    Start new flow
    流程登录    ${URL.Login}    ${User.UsernameJiaoyiyuan}    ${User.PasswordJiaoyiyuan}
    流程交易单管理-待申请付款Tab
    ${CurrentSec}    获取当前秒数
    流程交易单管理-待申请付款Tab-申请付款    ${testPicture}    ${CurrentSec}

流程审核付款正常
    流程申请付款正常
    Comment    Start new flow
    流程登录    ${URL.Login}    ${User.UsernameHangzhouowner}    ${User.PasswordHangzhouowner}
    流程交易单管理-待审核付款Tab
    流程交易单管理-待审核付款Tab-通过

流程打印付款正常
    流程审核付款正常
    Comment    Start new flow
    流程登录    ${URL.Login}    ${User.UsernameKuaiji}    ${User.PasswordKuaiji}
    流程交易单管理-待打印付款申请Tab    #进入待打印付款申请Tab
    流程交易单管理-待打印付款申请Tab-打印付款申请单
    关闭当前浏览器

流程确认付款正常
    流程打印付款正常
    Comment    Start new flow
    流程登录    ${URL.Login}    ${User.UsernameZongchuna}    ${User.PasswordZongchuna}
    流程交易单管理-待确认已付款Tab
    流程交易单管理-待确认已付款Tab-确认已付款

流程提货正常
    [Documentation]    1. 车牌号可以做成参数化，这里暂时用的是固定的
    流程确认付款正常
    Comment    Start new flow
    流程登录    ${URL.Login}    ${User.UsernameJiaoyiyuan}    ${User.PasswordJiaoyiyuan}
    流程交易单管理-待提货Tab
    流程交易单管理-待提货Tab-确认提交    浙A12345    ${testPicture}

流程放货正常
    流程提货正常
    Comment    Start new flow
    流程登录    ${URL.Login}    ${User.UsernameNeiqin}    ${User.PasswordNeiqin}
    流程交易单管理-待放货Tab
    流程交易单管理-待放货Tab-打印放货单
    关闭当前浏览器

流程二次结算正常
    流程放货正常
    Comment    Start new flow
    流程登录    ${URL.Login}    ${User.UsernameJiaoyiyuan}    ${User.PasswordJiaoyiyuan}
    流程交易单管理-待二次结算Tab
    流程交易单管理-待二次结算Tab-录入实提量    1    1
    关闭当前浏览器
    流程登录    ${URL.Login}    ${User.UsernameNeiqin}    ${User.PasswordNeiqin}
    流程交易单管理-待二次结算Tab
    流程交易单管理-待二次结算Tab-录入实提量    1    1
    单击按钮    xpath=//*[@id="secondary"]    #二次结算
    单击按钮    xpath=//*[@class="modal-content"]//button[@class="btn btn-primary"]    #单击浮层OK
    关闭当前浏览器
